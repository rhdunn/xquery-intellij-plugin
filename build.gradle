buildscript {
    // (1) Year and Version -- (a) IC-2017.3, (b) 2017.2.4, (c) 2018.1
    // (2) Build ID -- (a) IU-163.7890, (b) 171.1234
    // (3) Named Snapshots -- (a) LATEST-EAP-SNAPSHOT, (b) LATEST-TRUNK-SNAPSHOT
    ext.since_from_version = { version ->
        // (1) Year and Version

        def since = version =~ /([A-Z]+-)?20([0-9][0-9])\.([1-3])(\.[0-9]+)?$/
        if (since.matches()) {
            return "${since.group(2)}${since.group(3)}"
        }

        // (2) Build ID

        since = version =~ /([A-Z]+-)?([0-9][0-9][0-9])\.[0-9]+?$/
        if (since.matches()) {
            return since.group(2)
        }

        // (3) Named Snapshots

        def repositoryPath = "https://www.jetbrains.com/intellij-repository/snapshots/com/jetbrains/intellij/idea/BUILD"
        def build = new File("$projectDir/build/BUILD-${version}.txt")
        if (!build.exists()) {
            build.parentFile.mkdirs()
            println "Downloading build version file '$repositoryPath/$version/BUILD-${version}.txt' to '${build.absolutePath}'"
            new URL("$repositoryPath/$version/BUILD-${version}.txt").withInputStream { input ->
                build.withOutputStream { output -> (output << input).close() }
            }
        }

        return build.readLines()[0].split('\\.')[0]
    }

    ext.idea_version = System.getenv("IDEA_VERSION") ?: '2021.2'
    ext.idea_since_build = ext.since_from_version(ext.idea_version).toInteger()
    ext.idea_type = System.getenv("IDEA_TYPE") ?: 'IC'

    ext.kotlin_version = "1.4.32"
    ext.kotlin_stdlib = "kotlin-stdlib"
    ext.java_version = "11"

    ext.plugin_version = '1.10.0'
    // Suffix ordering:
    //    `'-snapshot'` -- for development builds
    //    `'-eap-###'`  -- for early access preview builds (`-eap-1`, `-eap-2`, `-eap-3`, etc.)
    //    `''`          -- for release builds
    ext.suffix = '-snapshot'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.jetbrains.intellij" version "1.3.1"
}

configure(allprojects - project(':src')) {
    apply plugin: 'org.jetbrains.intellij'
    apply plugin: 'idea'
    apply plugin: 'kotlin'
    apply plugin: 'jacoco'

    group 'uk.co.reecedunn.intellij.plugin.xquery'
    version = plugin_version + '-' + idea_since_build.toString() + suffix

    repositories {
        mavenCentral()
        mavenLocal()
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = java_version
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = java_version
        }
    }

    intellij {
        version = idea_version
        type = idea_type
    }

    runIde {
        maxHeapSize = "2g"
    }

    dependencies {
        compile "org.jetbrains.kotlin:$kotlin_stdlib:$kotlin_version"
        compile "org.jsoup:jsoup:1.12.1"
        compile "net.sf.saxon:Saxon-HE:9.9.1-7"

        testCompile "org.junit.jupiter:junit-jupiter-api:5.7.0"

        testRuntime("org.junit.platform:junit-platform-launcher:1.7.0")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:5.7.0")
    }

    tasks {
        // Using the IntelliJ gradle plugin to provide the IntelliJ platform SDK in
        // each subproject results in various deployment related tasks being added
        // to those subprojects. As this plugin only uses those tasks on the main
        // project, those tasks are disabled globally and only enabled for the
        // main project.
        buildPlugin.enabled = false
        prepareSandbox.enabled = false
        prepareTestingSandbox.enabled = false
        publishPlugin.enabled = false
        runIde.enabled = false
        verifyPlugin.enabled = false
        buildSearchableOptions.enabled = false
    }

    test {
        useJUnitPlatform()
    }
}

dependencies {
    compile project(':src:plugin-basex')
    compile project(':src:plugin-existdb')
    compile project(':src:plugin-marklogic')
    compile project(':src:plugin-saxon')
    compile project(':src:plugin-xijp')

    compile project(':src:lang-xproc')
    compile project(':src:lang-java')

    compile project(':src:plugin-expath')
    compile project(':src:plugin-exquery')
    compile project(':src:plugin-w3')

    testCompile project(':src:kotlin-hamcrest')
    testCompile project(':src:intellij-test')
}

intellij.plugins = [
    'coverage',
    'com.intellij.properties',
    'org.intellij.intelliLang',
    'java'
]

if (idea_since_build >= 203) {
    compileKotlin.kotlinOptions.useIR = true
}
if (idea_type == 'IU') {
    if (idea_since_build >= 193) {
        sourceSets.main.resources.srcDirs += 'src/main/resources-microservices/193'
    } else {
        sourceSets.main.resources.srcDirs += 'src/main/resources-microservices/compat'
    }
} else {
    sourceSets.main.resources.srcDirs += 'src/main/resources-microservices/compat'
}

tasks {
    // Using the IntelliJ gradle plugin to provide the IntelliJ platform SDK in
    // each subproject results in various deployment related tasks being added
    // to those subprojects. As this plugin only uses those tasks on the main
    // project, those tasks are disabled globally and only enabled for the
    // main project.
    buildPlugin.enabled = true
    prepareSandbox.enabled = true
    prepareTestingSandbox.enabled = true
    publishPlugin.enabled = true
    runIde.enabled = true
    verifyPlugin.enabled = true
    buildSearchableOptions.enabled = true
}

println "Building for IntelliJ ${idea_type} version '${idea_version}', since build '${idea_since_build}'"

intellij.updateSinceUntilBuild = true
intellij.sameSinceUntilBuild = true // 'until' is an open 'since' build

patchPluginXml {
    sinceBuild = "${idea_since_build}"
}

jacoco {
    toolVersion = '0.8.4'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

repositories {
    mavenCentral()
}

// Module Dependency Report =============================================================
// Derived from https://gist.github.com/nikialeksey/7cefae6b3104ce9a2c765197343bc436

task moduleDependencyReport {
    doLast {
        def file = new File("project-dependencies.dot")
        file.delete()
        file << "strict digraph \"Module Dependencies\" {\n"
        def deps = [:]
        buildDeps(deps, rootProject)
        deps.forEach { from, toList ->
            file << "\"$from\" [shape=none]\n"
            toList.forEach { to ->
                if (to != "kotlin-hamcrest" && to != "intellij-test") {
                    file << ("\"$from\" -> \"$to\"\n")
                }
            }
        }
        file << "}\n"
    }
}

def buildDeps(deps, project) {
    project.configurations
           .findAll { it.name == "compile" || it.name == "runtime" }
           .collectMany { it.dependencies }
           .findAll { it instanceof ProjectDependency }
           .each { to -> addOnce(deps, project.name, to.name) }

    project.childProjects.each { name, childProject -> buildDeps(deps, childProject) }
}

static def addOnce(map, key, value) {
    if (key in map.keySet()) {
        if (!(value in map[key])) {
            map[key].add(value)
        }
    } else {
        map[key] = [value]
    }

    if (!(value in map.keySet())) {
        map[value] = []
    }
}
