buildscript {
    ext.since_from_version = { version ->
        def since = version =~ /([A-Z]+-)?20([0-9][0-9])\.([1-3])(\.[0-9]+)?/ // e.g. IC-2017.3, 2017.2.4
        if (since.matches()) {
            return "${since.group(2)}${since.group(3)}"
        }
        since = version =~ /([A-Z]+-)?([0-9][0-9][0-9])\.[0-9]+?/ // e.g. IU-163.7890, 171.1234
        if (since.matches()) {
            return since.group(2)
        }
        if (version == "LATEST-EAP-SNAPSHOT") return "182"
        return "182" // version == "LATEST-TRUNK-SNAPSHOT"
    }

    ext.idea_version = System.getenv("IDEA_VERSION") ?: 'IC-2017.3'
    ext.idea_since_build = System.getenv("IDEA_SINCE_BUILD") ?: ext.since_from_version(ext.idea_version)
    ext.junit_milestone = ''
    ext.kotlin_version = '1.1.4-2'

    repositories {
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap-1.1' }
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.2.17"
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'kotlin'

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'jacoco'

    group 'uk.co.reecedunn.intellij.plugin.xquery'
    version '1.1'

    repositories {
        mavenCentral()
        mavenLocal()
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "org.junit.jupiter:junit-jupiter-api:5.0.0$junit_milestone"
    // See: http://junit.org/junit5/docs/snapshot/user-guide/#running-tests-ide-intellij-idea
    testRuntime("org.junit.platform:junit-platform-launcher:1.0.0$junit_milestone")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.0$junit_milestone")
    testRuntime("org.junit.vintage:junit-vintage-engine:4.12.0$junit_milestone")
}

intellij {
    version = idea_version
    println "Building for IntelliJ version '${version}', since build '${idea_since_build}'"

    updateSinceUntilBuild = true
    sameSinceUntilBuild = true // 'until' is an open 'since' build

    plugins 'coverage'  // Bundled plugin dependencies
    pluginName 'xquery-intellij-plugin'
}

patchPluginXml {
    sinceBuild = idea_since_build
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

repositories {
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap-1.1' }
    mavenCentral()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
